;------------------------------------------------------------------------------
; Подпрограммы для работы с MCP4921, MCP4922
; 12-битный ЦАП
; SPI интерфейс
; с возможностью изменения коэффициента передачи выходного буфера
; вход для внешнего источника опорного напряжения VREF
;
; Формат посылаемой команды:
; bit 15   = 0 - запись в регистр ЦАП канал A
;          = 1 - запись в регистр ЦАП канал B
; bit 14   = 1 - буферизованный вход Vref
;          = 0 - небуферизованный вход Vref
; bit 13   = 1 - 1x(Vout = Vref*D/4096)
;          = 0 - 2x(Vout = 2*Vref*D/4096)
; bit 12   = 1 - активный режим
;          = 0 - устройство выключено. Vout присоединен к резистору 500 кОм
; bit 11-0  D11:D0  биты данных ЦАП
; 
; Для MCP4921 bit 15 = 0
; 
; Перед использованием необходимо проинициализировать SPI
; 
; Задать DAC_CS_PORT, DAC_CS_DDR, DAC_CS
;
; (C) 2017-2020 Vitaliy Zinoviev
; https://github.com/nf-zvv/IVC_Tracer_v2
;
; History
; =======
;
;------------------------------------------------------------------------------
#ifndef _MCP492X_ASM_
#define _MCP492X_ASM_

.ifndef __zero_reg__
.def __zero_reg__ = r2
.endif

.equ	DAC_CS_PORT	= PORTD
.equ	DAC_CS_DDR	= DDRD
.equ	DAC_CS		= PD6

.equ	DAC_A       = 0x00
.equ	DAC_B       = 0x80

.equ	Buffer_on   = 0x40
.equ	Buffer_off  = 0x00

.equ	Gain_1X	    = 0x20
.equ	Gain_2X	    = 0x00

.equ	Active      = 0x10
.equ	Shutdown    = 0x00


.dseg
DAC_CH_A:		.byte	2
DAC_CH_B:		.byte	2
.cseg

;------------------------------------------------------------------------------
; Инициализация ЦАП MCP4921
; 
; Вызовы: DAC_SET
; Используются: r16*, r17*
; Вход: -
; Выход: -
;------------------------------------------------------------------------------
DAC_INIT:
			; настройка линий ввода/вывода
			sbi		DAC_CS_DDR,DAC_CS	; DAC CS output
			sbi		DAC_CS_PORT,DAC_CS	; CS=1
			; Устанавливаем начальное значение ЦАП
			sts		DAC_CH_A+0,__zero_reg__
			sts		DAC_CH_A+1,__zero_reg__
			rcall	DAC_SET_A
			sts		DAC_CH_B+0,__zero_reg__
			sts		DAC_CH_B+1,__zero_reg__
			rcall	DAC_SET_B
			ret


;------------------------------------------------------------------------------
; Установка значения ЦАП MCP4921/MCP4921
; Канал A
; 
; Вызовы: SPI_RW
; Используются: r16*
; Вход: DAC_A
; Выход: -
;------------------------------------------------------------------------------
DAC_SET_A:
			cbi		DAC_CS_PORT,DAC_CS	; CS=0
			lds		r16,DAC_CH_A+1			; старший байт
			;andi	r16,0b00001111
			ori		r16,(DAC_A|Buffer_on|Gain_1X|Active)	; буферизованный вход Vref + GAIN 1x
			rcall	SPI_RW
			lds		r16,DAC_CH_A+0			; младший байт
			rcall	SPI_RW
			sbi		DAC_CS_PORT,DAC_CS	; CS=1
			ret

;------------------------------------------------------------------------------
; Установка значения ЦАП MCP4922
; Канал B
; 
; Вызовы: SPI_RW
; Используются: r16*
; Вход: DAC_A
; Выход: -
;------------------------------------------------------------------------------
DAC_SET_B:
			cbi		DAC_CS_PORT,DAC_CS	; CS=0
			lds		r16,DAC_CH_B+1			; старший байт
			;andi	r16,0b00001111
			ori		r16,(DAC_B|Buffer_on|Gain_1X|Active)	; буферизованный вход Vref + GAIN 1x
			rcall	SPI_RW
			lds		r16,DAC_CH_B+0			; младший байт
			rcall	SPI_RW
			sbi		DAC_CS_PORT,DAC_CS	; CS=1
			ret

#endif  /* _MCP492X_ASM_ */

;------------------------------------------------------------------------------
; End of file
;------------------------------------------------------------------------------
